/*  
 *  Copyright 2017 Marcelo Grossi
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0*
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/avast/maven/'
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.9.3"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.10.1"
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'docker-compose'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
    tag {
        initialVersion = {config, position ->  "0.2.0" }
    }
}

group = "com.github.magrossi"
archivesBaseName = "log4j2-elasticsearch-appender"
version = scmVersion.version

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.8.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.8.2'

	implementation 'org.elasticsearch.client:rest:5.5.2'

    testImplementation 'org.assertj:assertj-core:3.12.2'
	testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.9.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.4'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.4'
}

def releaseRepo = project.hasProperty("ossrhReleasesRepo") ? ossrhReleasesRepo : System.getenv("ossrhReleasesRepo")
def snapshotRepo = project.hasProperty("ossrhSnapshotsRepo") ? ossrhSnapshotsRepo : System.getenv("ossrhSnapshotsRepo")
def repoUser = project.hasProperty("ossrhUsername") ? ossrhUsername : System.getenv("ossrhUsername")
def repoPwd = project.hasProperty("ossrhPassword") ? ossrhPassword : System.getenv("ossrhPassword")

signing {
    sign configurations.archives
}

if ("true" == System.getenv("TRAVIS")) {
    signArchives.enabled = false
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
dockerCompose.isRequiredBy test

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: releaseRepo) {
                authentication(userName: repoUser, password: repoPwd)
            }

            snapshotRepository(url: snapshotRepo) {
                authentication(userName: repoUser, password: repoPwd)
            }

            pom.project {
                name 'Log4J2 ElasticSearch REST Appender'
                packaging 'jar'
                description 'An ElasticSearch appender for Log4J2.'
                url 'https://github.com/magrossi/es-log4j2-appender'

                scm {
                    connection 'scm:git:git@github.com:magrossi/es-log4j2-appender.git'
                    developerConnection 'scm:git:git@github.com:magrossi/es-log4j2-appender.git'
                    url 'git@github.com:magrossi/es-log4j2-appender.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'magrossi'
                        name 'Marcelo Grossi'
                        email 'magrossi@gmail.com'
                    }
                }
            }
        }
    }
}
